#!/usr/bin/env perl

use strict;
use warnings;
use 5.014;
use Date::Format qw(time2str);
use HTML::Template;
use Storable qw(nstore retrieve);
use WWW::Mechanize;

our $VERSION = '0.0';

my $now = time;
my $www = WWW::Mechanize->new;
my $outfile = $ARGV[0] // '/dev/stdout';
my $db_file = 'linktrack_db';
my %db;

sub load_db {
	if (-e $db_file) {
		my $db = retrieve($db_file);
		%db = %{$db};
	}
}

sub save_db {
	nstore(\%db, $db_file);
}

sub grab_links {
	my ($lecture, $url) = @_;

	$db{lectures}{$lecture} = $url;

	$www->get($url);

	for my $link ($www->links) {
		my $url = $link->url_abs->abs;
		my $text = $link->text;

		if (not defined $text or $text eq q{}) {
			next;
		}

		$db{entries}{$now}{$lecture}{$url} = $text;

	}
}

sub write_html {
	my $template = HTML::Template->new(
		filename => 'share/index.html',
	);

	my $prev;
	my @dates;

	for my $time (sort keys $db{entries}) {
		say "\n$time";
		my $date = time2str('%d.%m.%Y', $time);
		my @changes;
		for my $lecture (sort keys $db{entries}{$time}) {
			for my $url (sort keys $db{entries}{$time}{$lecture}) {
				if (not exists $prev->{$lecture}{$url}) {
					push(@changes, {
						lecture => "<a href=\"$db{lectures}{$lecture}\">$lecture</a>",
						type => "+",
						link => "<a href=\"$url\">$db{entries}{$time}{$lecture}{$url}</a>",
					});
				}
			}
		}
		push(@dates, {date => $date, changes => \@changes});
		$prev = $db{entries}{$time};
	}
	@dates = reverse @dates;
	$template->param(
		version => $VERSION,
		dates => \@dates,
	);
	open (my $fh, '>', $outfile);
	$template->output(print_to => $fh);
	close($fh);
}

load_db;

open(my $fh, '<', 'lectures');
for my $line (<$fh>) {
	chomp $line;
	my ($name, $url) = split(/ /, $line);
	grab_links($name, $url);
}

write_html;
save_db;

__END__

=head1 NAME

=head1 SYNOPSIS

=head1 VERSION

=head1 DESCRIPTION

=head1 OPTIONS

=over

=back

=head1 EXIT STATUS

=head1 CONFIGURATION

None.

=head1 DEPENDENCIES

=over

=back

=head1 BUGS AND LIMITATIONS

=head1 AUTHOR

Copyright (C) 2012 by Daniel Friesel E<lt>derf@finalrewind.orgE<gt>

=head1 LICENSE

  0. You just DO WHAT THE FUCK YOU WANT TO.
